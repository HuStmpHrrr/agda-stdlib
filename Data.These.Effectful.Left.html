<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.These.Effectful.Left</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Left-biased universe-sensitive functor and monad instances for These.</a>
<a id="179" class="Comment">--</a>
<a id="182" class="Comment">------------------------------------------------------------------------</a>

<a id="256" class="Comment">-- To minimize the universe level of the RawFunctor, we require that</a>
<a id="325" class="Comment">-- elements of B are &quot;lifted&quot; to a copy of B at a higher universe level</a>
<a id="397" class="Comment">-- (a ⊔ b).</a>
<a id="409" class="Comment">-- See the Data.Product.Effectful.Examples for how this is done in a</a>
<a id="478" class="Comment">-- Product-based similar setting.</a>

<a id="513" class="Comment">-- This functor can be understood as a notion of computation which can</a>
<a id="584" class="Comment">-- either fail (this), succeed (that) or accumulate warnings whilst</a>
<a id="652" class="Comment">-- delivering a successful computation (these).</a>

<a id="701" class="Comment">-- It is a good alternative to Data.Product.Effectful when the notion</a>
<a id="771" class="Comment">-- of warnings does not have a neutral element (e.g. List⁺).</a>

<a id="833" class="Symbol">{-#</a> <a id="837" class="Keyword">OPTIONS</a> <a id="845" class="Pragma">--without-K</a> <a id="857" class="Pragma">--safe</a> <a id="864" class="Symbol">#-}</a>

<a id="869" class="Keyword">open</a> <a id="874" class="Keyword">import</a> <a id="881" href="Level.html" class="Module">Level</a>
<a id="887" class="Keyword">open</a> <a id="892" class="Keyword">import</a> <a id="899" href="Algebra.html" class="Module">Algebra</a>

<a id="908" class="Keyword">module</a> <a id="915" href="Data.These.Effectful.Left.html" class="Module">Data.These.Effectful.Left</a> <a id="941" class="Symbol">{</a><a id="942" href="Data.These.Effectful.Left.html#942" class="Bound">c</a> <a id="944" href="Data.These.Effectful.Left.html#944" class="Bound">ℓ</a><a id="945" class="Symbol">}</a> <a id="947" class="Symbol">(</a><a id="948" href="Data.These.Effectful.Left.html#948" class="Bound">W</a> <a id="950" class="Symbol">:</a> <a id="952" href="Algebra.Bundles.html#2143" class="Record">Semigroup</a> <a id="962" href="Data.These.Effectful.Left.html#942" class="Bound">c</a> <a id="964" href="Data.These.Effectful.Left.html#944" class="Bound">ℓ</a><a id="965" class="Symbol">)</a> <a id="967" class="Symbol">(</a><a id="968" href="Data.These.Effectful.Left.html#968" class="Bound">b</a> <a id="970" class="Symbol">:</a> <a id="972" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="977" class="Symbol">)</a> <a id="979" class="Keyword">where</a>

<a id="986" class="Keyword">open</a> <a id="991" href="Algebra.Bundles.html#2143" class="Module">Semigroup</a> <a id="1001" href="Data.These.Effectful.Left.html#948" class="Bound">W</a>
<a id="1003" class="Keyword">open</a> <a id="1008" class="Keyword">import</a> <a id="1015" href="Data.These.Effectful.Left.Base.html" class="Module">Data.These.Effectful.Left.Base</a> <a id="1046" href="Algebra.Bundles.html#2225" class="Function">Carrier</a> <a id="1054" href="Data.These.Effectful.Left.html#968" class="Bound">b</a> <a id="1056" class="Keyword">public</a>

<a id="1064" class="Keyword">open</a> <a id="1069" class="Keyword">import</a> <a id="1076" href="Data.These.Base.html" class="Module">Data.These.Base</a>
<a id="1092" class="Keyword">open</a> <a id="1097" class="Keyword">import</a> <a id="1104" href="Effect.Applicative.html" class="Module">Effect.Applicative</a>
<a id="1123" class="Keyword">open</a> <a id="1128" class="Keyword">import</a> <a id="1135" href="Effect.Monad.html" class="Module">Effect.Monad</a>

<a id="1149" class="Keyword">module</a> <a id="1156" href="Data.These.Effectful.Left.html#1156" class="Module">_</a> <a id="1158" class="Symbol">{</a><a id="1159" href="Data.These.Effectful.Left.html#1159" class="Bound">a</a> <a id="1161" href="Data.These.Effectful.Left.html#1161" class="Bound">b</a><a id="1162" class="Symbol">}</a> <a id="1164" class="Symbol">{</a><a id="1165" href="Data.These.Effectful.Left.html#1165" class="Bound">A</a> <a id="1167" class="Symbol">:</a> <a id="1169" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1173" href="Data.These.Effectful.Left.html#1159" class="Bound">a</a><a id="1174" class="Symbol">}</a> <a id="1176" class="Symbol">{</a><a id="1177" href="Data.These.Effectful.Left.html#1177" class="Bound">B</a> <a id="1179" class="Symbol">:</a> <a id="1181" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1185" href="Data.These.Effectful.Left.html#1161" class="Bound">b</a><a id="1186" class="Symbol">}</a> <a id="1188" class="Keyword">where</a>

<a id="applicative"></a><a id="1195" href="Data.These.Effectful.Left.html#1195" class="Function">applicative</a> <a id="1207" class="Symbol">:</a> <a id="1209" href="Effect.Applicative.html#490" class="Function">RawApplicative</a> <a id="1224" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a>
<a id="1231" href="Data.These.Effectful.Left.html#1195" class="Function">applicative</a> <a id="1243" class="Symbol">=</a> <a id="1245" class="Keyword">record</a>
  <a id="1254" class="Symbol">{</a> <a id="1256" href="Effect.Applicative.Indexed.html#999" class="Field">pure</a> <a id="1261" class="Symbol">=</a> <a id="1263" href="Data.These.Base.html#614" class="InductiveConstructor">that</a>
  <a id="1270" class="Symbol">;</a> <a id="1272" href="Effect.Applicative.Indexed.html#1030" class="Field Operator">_⊛_</a>  <a id="1277" class="Symbol">=</a> <a id="1279" href="Data.These.Effectful.Left.html#1295" class="Function">ap</a>
  <a id="1284" class="Symbol">}</a> <a id="1286" class="Keyword">where</a>

  <a id="1295" href="Data.These.Effectful.Left.html#1295" class="Function">ap</a> <a id="1298" class="Symbol">:</a> <a id="1300" class="Symbol">∀</a> <a id="1302" class="Symbol">{</a><a id="1303" href="Data.These.Effectful.Left.html#1303" class="Bound">A</a> <a id="1305" href="Data.These.Effectful.Left.html#1305" class="Bound">B</a><a id="1306" class="Symbol">}→</a> <a id="1309" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1316" class="Symbol">(</a><a id="1317" href="Data.These.Effectful.Left.html#1303" class="Bound">A</a> <a id="1319" class="Symbol">→</a> <a id="1321" href="Data.These.Effectful.Left.html#1305" class="Bound">B</a><a id="1322" class="Symbol">)</a> <a id="1324" class="Symbol">→</a> <a id="1326" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1333" href="Data.These.Effectful.Left.html#1303" class="Bound">A</a> <a id="1335" class="Symbol">→</a> <a id="1337" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1344" href="Data.These.Effectful.Left.html#1305" class="Bound">B</a>
  <a id="1348" href="Data.These.Effectful.Left.html#1295" class="Function">ap</a> <a id="1351" class="Symbol">(</a><a id="1352" href="Data.These.Base.html#586" class="InductiveConstructor">this</a> <a id="1357" href="Data.These.Effectful.Left.html#1357" class="Bound">w</a><a id="1358" class="Symbol">)</a>    <a id="1363" href="Data.These.Effectful.Left.html#1363" class="Bound">t</a> <a id="1365" class="Symbol">=</a> <a id="1367" href="Data.These.Base.html#586" class="InductiveConstructor">this</a> <a id="1372" href="Data.These.Effectful.Left.html#1357" class="Bound">w</a>
  <a id="1376" href="Data.These.Effectful.Left.html#1295" class="Function">ap</a> <a id="1379" class="Symbol">(</a><a id="1380" href="Data.These.Base.html#614" class="InductiveConstructor">that</a> <a id="1385" href="Data.These.Effectful.Left.html#1385" class="Bound">f</a><a id="1386" class="Symbol">)</a>    <a id="1391" href="Data.These.Effectful.Left.html#1391" class="Bound">t</a> <a id="1393" class="Symbol">=</a> <a id="1395" href="Data.These.Base.html#1058" class="Function">map₂</a> <a id="1400" href="Data.These.Effectful.Left.html#1385" class="Bound">f</a> <a id="1402" href="Data.These.Effectful.Left.html#1391" class="Bound">t</a>
  <a id="1406" href="Data.These.Effectful.Left.html#1295" class="Function">ap</a> <a id="1409" class="Symbol">(</a><a id="1410" href="Data.These.Base.html#642" class="InductiveConstructor">these</a> <a id="1416" href="Data.These.Effectful.Left.html#1416" class="Bound">w</a> <a id="1418" href="Data.These.Effectful.Left.html#1418" class="Bound">f</a><a id="1419" class="Symbol">)</a> <a id="1421" href="Data.These.Effectful.Left.html#1421" class="Bound">t</a> <a id="1423" class="Symbol">=</a> <a id="1425" href="Data.These.Base.html#835" class="Function">map</a> <a id="1429" class="Symbol">(</a><a id="1430" href="Data.These.Effectful.Left.html#1416" class="Bound">w</a> <a id="1432" href="Algebra.Bundles.html#2281" class="Function Operator">∙_</a><a id="1434" class="Symbol">)</a> <a id="1436" href="Data.These.Effectful.Left.html#1418" class="Bound">f</a> <a id="1438" href="Data.These.Effectful.Left.html#1421" class="Bound">t</a>

<a id="monad"></a><a id="1441" href="Data.These.Effectful.Left.html#1441" class="Function">monad</a> <a id="1447" class="Symbol">:</a> <a id="1449" href="Effect.Monad.html#444" class="Function">RawMonad</a> <a id="1458" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a>
<a id="1465" href="Data.These.Effectful.Left.html#1441" class="Function">monad</a> <a id="1471" class="Symbol">=</a> <a id="1473" class="Keyword">record</a>
  <a id="1482" class="Symbol">{</a> <a id="1484" href="Effect.Monad.Indexed.html#640" class="Field">return</a> <a id="1491" class="Symbol">=</a> <a id="1493" href="Data.These.Base.html#614" class="InductiveConstructor">that</a>
  <a id="1500" class="Symbol">;</a> <a id="1502" href="Effect.Monad.Indexed.html#673" class="Field Operator">_&gt;&gt;=_</a>  <a id="1509" class="Symbol">=</a> <a id="1511" href="Data.These.Effectful.Left.html#1529" class="Function">bind</a>
  <a id="1518" class="Symbol">}</a> <a id="1520" class="Keyword">where</a>

  <a id="1529" href="Data.These.Effectful.Left.html#1529" class="Function">bind</a> <a id="1534" class="Symbol">:</a> <a id="1536" class="Symbol">∀</a> <a id="1538" class="Symbol">{</a><a id="1539" href="Data.These.Effectful.Left.html#1539" class="Bound">A</a> <a id="1541" href="Data.These.Effectful.Left.html#1541" class="Bound">B</a><a id="1542" class="Symbol">}</a> <a id="1544" class="Symbol">→</a> <a id="1546" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1553" href="Data.These.Effectful.Left.html#1539" class="Bound">A</a> <a id="1555" class="Symbol">→</a> <a id="1557" class="Symbol">(</a><a id="1558" href="Data.These.Effectful.Left.html#1539" class="Bound">A</a> <a id="1560" class="Symbol">→</a> <a id="1562" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1569" href="Data.These.Effectful.Left.html#1541" class="Bound">B</a><a id="1570" class="Symbol">)</a> <a id="1572" class="Symbol">→</a> <a id="1574" href="Data.These.Effectful.Left.Base.html#801" class="Function">Theseₗ</a> <a id="1581" href="Data.These.Effectful.Left.html#1541" class="Bound">B</a>
  <a id="1585" href="Data.These.Effectful.Left.html#1529" class="Function">bind</a> <a id="1590" class="Symbol">(</a><a id="1591" href="Data.These.Base.html#586" class="InductiveConstructor">this</a> <a id="1596" href="Data.These.Effectful.Left.html#1596" class="Bound">w</a><a id="1597" class="Symbol">)</a>    <a id="1602" href="Data.These.Effectful.Left.html#1602" class="Bound">f</a> <a id="1604" class="Symbol">=</a> <a id="1606" href="Data.These.Base.html#586" class="InductiveConstructor">this</a> <a id="1611" href="Data.These.Effectful.Left.html#1596" class="Bound">w</a>
  <a id="1615" href="Data.These.Effectful.Left.html#1529" class="Function">bind</a> <a id="1620" class="Symbol">(</a><a id="1621" href="Data.These.Base.html#614" class="InductiveConstructor">that</a> <a id="1626" href="Data.These.Effectful.Left.html#1626" class="Bound">t</a><a id="1627" class="Symbol">)</a>    <a id="1632" href="Data.These.Effectful.Left.html#1632" class="Bound">f</a> <a id="1634" class="Symbol">=</a> <a id="1636" href="Data.These.Effectful.Left.html#1632" class="Bound">f</a> <a id="1638" href="Data.These.Effectful.Left.html#1626" class="Bound">t</a>
  <a id="1642" href="Data.These.Effectful.Left.html#1529" class="Function">bind</a> <a id="1647" class="Symbol">(</a><a id="1648" href="Data.These.Base.html#642" class="InductiveConstructor">these</a> <a id="1654" href="Data.These.Effectful.Left.html#1654" class="Bound">w</a> <a id="1656" href="Data.These.Effectful.Left.html#1656" class="Bound">t</a><a id="1657" class="Symbol">)</a> <a id="1659" href="Data.These.Effectful.Left.html#1659" class="Bound">f</a> <a id="1661" class="Symbol">=</a> <a id="1663" href="Data.These.Base.html#996" class="Function">map₁</a> <a id="1668" class="Symbol">(</a><a id="1669" href="Data.These.Effectful.Left.html#1654" class="Bound">w</a> <a id="1671" href="Algebra.Bundles.html#2281" class="Function Operator">∙_</a><a id="1673" class="Symbol">)</a> <a id="1675" class="Symbol">(</a><a id="1676" href="Data.These.Effectful.Left.html#1659" class="Bound">f</a> <a id="1678" href="Data.These.Effectful.Left.html#1656" class="Bound">t</a><a id="1679" class="Symbol">)</a>
</pre></body></html>