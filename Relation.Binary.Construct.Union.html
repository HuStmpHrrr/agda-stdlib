<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Construct.Union</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Union of two binary relations</a>
<a id="139" class="Comment">------------------------------------------------------------------------</a>

<a id="213" class="Symbol">{-#</a> <a id="217" class="Keyword">OPTIONS</a> <a id="225" class="Pragma">--without-K</a> <a id="237" class="Pragma">--safe</a> <a id="244" class="Symbol">#-}</a>

<a id="249" class="Keyword">module</a> <a id="256" href="Relation.Binary.Construct.Union.html" class="Module">Relation.Binary.Construct.Union</a> <a id="288" class="Keyword">where</a>

<a id="295" class="Keyword">open</a> <a id="300" class="Keyword">import</a> <a id="307" href="Data.Product.html" class="Module">Data.Product</a>
<a id="320" class="Keyword">open</a> <a id="325" class="Keyword">import</a> <a id="332" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="346" class="Symbol">as</a> <a id="349" class="Module">Sum</a>
<a id="353" class="Keyword">open</a> <a id="358" class="Keyword">import</a> <a id="365" href="Function.Base.html" class="Module">Function.Base</a> <a id="379" class="Keyword">using</a> <a id="385" class="Symbol">(</a><a id="386" href="Function.Base.html#1106" class="Function Operator">_∘_</a><a id="389" class="Symbol">)</a>
<a id="391" class="Keyword">open</a> <a id="396" class="Keyword">import</a> <a id="403" href="Level.html" class="Module">Level</a> <a id="409" class="Keyword">using</a> <a id="415" class="Symbol">(</a><a id="416" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="421" class="Symbol">;</a> <a id="423" href="Agda.Primitive.html#810" class="Primitive Operator">_⊔_</a><a id="426" class="Symbol">)</a>
<a id="428" class="Keyword">open</a> <a id="433" class="Keyword">import</a> <a id="440" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="456" class="Keyword">open</a> <a id="461" class="Keyword">import</a> <a id="468" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="485" class="Keyword">using</a> <a id="491" class="Symbol">(</a><a id="492" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a><a id="495" class="Symbol">;</a> <a id="497" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a><a id="499" class="Symbol">)</a>
<a id="501" class="Keyword">open</a> <a id="506" class="Keyword">import</a> <a id="513" href="Relation.Nullary.Sum.html" class="Module">Relation.Nullary.Sum</a> <a id="534" class="Keyword">using</a> <a id="540" class="Symbol">(</a><a id="541" href="Relation.Nullary.Sum.html#946" class="Function Operator">_⊎-dec_</a><a id="548" class="Symbol">)</a>

<a id="551" class="Keyword">private</a>
  <a id="561" class="Keyword">variable</a>
    <a id="574" href="Relation.Binary.Construct.Union.html#574" class="Generalizable">a</a> <a id="576" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">b</a> <a id="578" href="Relation.Binary.Construct.Union.html#578" class="Generalizable">ℓ</a> <a id="580" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a> <a id="583" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a> <a id="586" href="Relation.Binary.Construct.Union.html#586" class="Generalizable">ℓ₃</a> <a id="589" class="Symbol">:</a> <a id="591" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="601" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="603" class="Symbol">:</a> <a id="605" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="609" href="Relation.Binary.Construct.Union.html#574" class="Generalizable">a</a>
    <a id="615" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="617" class="Symbol">:</a> <a id="619" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="623" href="Relation.Binary.Construct.Union.html#576" class="Generalizable">b</a>

<a id="626" class="Comment">------------------------------------------------------------------------</a>
<a id="699" class="Comment">-- Definition</a>

<a id="_∪_"></a><a id="714" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">_∪_</a> <a id="718" class="Symbol">:</a> <a id="720" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="724" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="726" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="728" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a> <a id="731" class="Symbol">→</a> <a id="733" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="737" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="739" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="741" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a> <a id="744" class="Symbol">→</a> <a id="746" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="750" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="752" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="754" class="Symbol">(</a><a id="755" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a> <a id="758" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="760" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="762" class="Symbol">)</a>
<a id="764" href="Relation.Binary.Construct.Union.html#764" class="Bound">L</a> <a id="766" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="768" href="Relation.Binary.Construct.Union.html#768" class="Bound">R</a> <a id="770" class="Symbol">=</a> <a id="772" class="Symbol">λ</a> <a id="774" href="Relation.Binary.Construct.Union.html#774" class="Bound">i</a> <a id="776" href="Relation.Binary.Construct.Union.html#776" class="Bound">j</a> <a id="778" class="Symbol">→</a> <a id="780" href="Relation.Binary.Construct.Union.html#764" class="Bound">L</a> <a id="782" href="Relation.Binary.Construct.Union.html#774" class="Bound">i</a> <a id="784" href="Relation.Binary.Construct.Union.html#776" class="Bound">j</a> <a id="786" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="788" href="Relation.Binary.Construct.Union.html#768" class="Bound">R</a> <a id="790" href="Relation.Binary.Construct.Union.html#774" class="Bound">i</a> <a id="792" href="Relation.Binary.Construct.Union.html#776" class="Bound">j</a>

<a id="795" class="Comment">------------------------------------------------------------------------</a>
<a id="868" class="Comment">-- Properties</a>

<a id="883" class="Keyword">module</a> <a id="890" href="Relation.Binary.Construct.Union.html#890" class="Module">_</a> <a id="892" class="Symbol">(</a><a id="893" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="895" class="Symbol">:</a> <a id="897" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="901" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="903" href="Relation.Binary.Construct.Union.html#578" class="Generalizable">ℓ</a><a id="904" class="Symbol">)</a> <a id="906" class="Symbol">(</a><a id="907" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a> <a id="909" class="Symbol">:</a> <a id="911" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="915" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="917" href="Relation.Binary.Construct.Union.html#578" class="Generalizable">ℓ</a><a id="918" class="Symbol">)</a> <a id="920" class="Keyword">where</a>

  <a id="929" href="Relation.Binary.Construct.Union.html#929" class="Function">reflexive</a> <a id="939" class="Symbol">:</a> <a id="941" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="951" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="953" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="955" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="965" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a> <a id="967" class="Symbol">→</a> <a id="969" href="Relation.Binary.Definitions.html#1339" class="Function">Reflexive</a> <a id="979" class="Symbol">(</a><a id="980" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="982" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="984" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a><a id="985" class="Symbol">)</a>
  <a id="989" href="Relation.Binary.Construct.Union.html#929" class="Function">reflexive</a> <a id="999" class="Symbol">(</a><a id="1000" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1005" href="Relation.Binary.Construct.Union.html#1005" class="Bound">L-refl</a><a id="1011" class="Symbol">)</a> <a id="1013" class="Symbol">=</a> <a id="1015" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1020" href="Relation.Binary.Construct.Union.html#1005" class="Bound">L-refl</a>
  <a id="1029" href="Relation.Binary.Construct.Union.html#929" class="Function">reflexive</a> <a id="1039" class="Symbol">(</a><a id="1040" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1045" href="Relation.Binary.Construct.Union.html#1045" class="Bound">R-refl</a><a id="1051" class="Symbol">)</a> <a id="1053" class="Symbol">=</a> <a id="1055" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1060" href="Relation.Binary.Construct.Union.html#1045" class="Bound">R-refl</a>

  <a id="1070" href="Relation.Binary.Construct.Union.html#1070" class="Function">total</a> <a id="1076" class="Symbol">:</a> <a id="1078" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1084" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1086" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="1088" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1094" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a> <a id="1096" class="Symbol">→</a> <a id="1098" href="Relation.Binary.Definitions.html#2584" class="Function">Total</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1107" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1109" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a><a id="1110" class="Symbol">)</a>
  <a id="1114" href="Relation.Binary.Construct.Union.html#1070" class="Function">total</a> <a id="1120" class="Symbol">(</a><a id="1121" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1126" href="Relation.Binary.Construct.Union.html#1126" class="Bound">L-total</a><a id="1133" class="Symbol">)</a> <a id="1135" href="Relation.Binary.Construct.Union.html#1135" class="Bound">x</a> <a id="1137" href="Relation.Binary.Construct.Union.html#1137" class="Bound">y</a> <a id="1139" class="Symbol">=</a> <a id="1141" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1143" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1148" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1150" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1155" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1157" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1162" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1164" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1169" href="Data.Sum.Base.html#920" class="Function Operator">]</a> <a id="1171" class="Symbol">(</a><a id="1172" href="Relation.Binary.Construct.Union.html#1126" class="Bound">L-total</a> <a id="1180" href="Relation.Binary.Construct.Union.html#1135" class="Bound">x</a> <a id="1182" href="Relation.Binary.Construct.Union.html#1137" class="Bound">y</a><a id="1183" class="Symbol">)</a>
  <a id="1187" href="Relation.Binary.Construct.Union.html#1070" class="Function">total</a> <a id="1193" class="Symbol">(</a><a id="1194" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1199" href="Relation.Binary.Construct.Union.html#1199" class="Bound">R-total</a><a id="1206" class="Symbol">)</a> <a id="1208" href="Relation.Binary.Construct.Union.html#1208" class="Bound">x</a> <a id="1210" href="Relation.Binary.Construct.Union.html#1210" class="Bound">y</a> <a id="1212" class="Symbol">=</a> <a id="1214" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1216" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1221" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1223" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1228" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1230" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1235" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1237" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1242" href="Data.Sum.Base.html#920" class="Function Operator">]</a> <a id="1244" class="Symbol">(</a><a id="1245" href="Relation.Binary.Construct.Union.html#1199" class="Bound">R-total</a> <a id="1253" href="Relation.Binary.Construct.Union.html#1208" class="Bound">x</a> <a id="1255" href="Relation.Binary.Construct.Union.html#1210" class="Bound">y</a><a id="1256" class="Symbol">)</a>

  <a id="1261" href="Relation.Binary.Construct.Union.html#1261" class="Function">min</a> <a id="1265" class="Symbol">:</a> <a id="1267" class="Symbol">∀</a> <a id="1269" class="Symbol">{</a><a id="1270" href="Relation.Binary.Construct.Union.html#1270" class="Bound">⊤</a><a id="1271" class="Symbol">}</a> <a id="1273" class="Symbol">→</a> <a id="1275" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1283" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1285" href="Relation.Binary.Construct.Union.html#1270" class="Bound">⊤</a> <a id="1287" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="1289" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1297" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a> <a id="1299" href="Relation.Binary.Construct.Union.html#1270" class="Bound">⊤</a> <a id="1301" class="Symbol">→</a> <a id="1303" href="Relation.Binary.Definitions.html#3337" class="Function">Minimum</a> <a id="1311" class="Symbol">(</a><a id="1312" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1314" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1316" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a><a id="1317" class="Symbol">)</a> <a id="1319" href="Relation.Binary.Construct.Union.html#1270" class="Bound">⊤</a>
  <a id="1323" href="Relation.Binary.Construct.Union.html#1261" class="Function">min</a> <a id="1327" class="Symbol">=</a> <a id="1329" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1331" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1336" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="1339" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1341" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1346" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="1349" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

  <a id="1354" href="Relation.Binary.Construct.Union.html#1354" class="Function">max</a> <a id="1358" class="Symbol">:</a> <a id="1360" class="Symbol">∀</a> <a id="1362" class="Symbol">{</a><a id="1363" href="Relation.Binary.Construct.Union.html#1363" class="Bound">⊥</a><a id="1364" class="Symbol">}</a> <a id="1366" class="Symbol">→</a> <a id="1368" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1376" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1378" href="Relation.Binary.Construct.Union.html#1363" class="Bound">⊥</a> <a id="1380" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="1382" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1390" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a> <a id="1392" href="Relation.Binary.Construct.Union.html#1363" class="Bound">⊥</a> <a id="1394" class="Symbol">→</a> <a id="1396" href="Relation.Binary.Definitions.html#3188" class="Function">Maximum</a> <a id="1404" class="Symbol">(</a><a id="1405" href="Relation.Binary.Construct.Union.html#893" class="Bound">L</a> <a id="1407" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1409" href="Relation.Binary.Construct.Union.html#907" class="Bound">R</a><a id="1410" class="Symbol">)</a> <a id="1412" href="Relation.Binary.Construct.Union.html#1363" class="Bound">⊥</a>
  <a id="1416" href="Relation.Binary.Construct.Union.html#1354" class="Function">max</a> <a id="1420" class="Symbol">=</a> <a id="1422" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1424" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1429" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="1432" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1434" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1439" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="1442" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

<a id="1445" class="Keyword">module</a> <a id="1452" href="Relation.Binary.Construct.Union.html#1452" class="Module">_</a> <a id="1454" class="Symbol">{</a><a id="1455" href="Relation.Binary.Construct.Union.html#1455" class="Bound">L</a> <a id="1457" class="Symbol">:</a> <a id="1459" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="1463" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1465" href="Relation.Binary.Construct.Union.html#578" class="Generalizable">ℓ</a><a id="1466" class="Symbol">}</a> <a id="1468" class="Symbol">{</a><a id="1469" href="Relation.Binary.Construct.Union.html#1469" class="Bound">R</a> <a id="1471" class="Symbol">:</a> <a id="1473" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="1477" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1479" href="Relation.Binary.Construct.Union.html#578" class="Generalizable">ℓ</a><a id="1480" class="Symbol">}</a> <a id="1482" class="Keyword">where</a>

  <a id="1491" href="Relation.Binary.Construct.Union.html#1491" class="Function">symmetric</a> <a id="1501" class="Symbol">:</a> <a id="1503" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1513" href="Relation.Binary.Construct.Union.html#1455" class="Bound">L</a> <a id="1515" class="Symbol">→</a> <a id="1517" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1527" href="Relation.Binary.Construct.Union.html#1469" class="Bound">R</a> <a id="1529" class="Symbol">→</a> <a id="1531" href="Relation.Binary.Definitions.html#1498" class="Function">Symmetric</a> <a id="1541" class="Symbol">(</a><a id="1542" href="Relation.Binary.Construct.Union.html#1455" class="Bound">L</a> <a id="1544" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1546" href="Relation.Binary.Construct.Union.html#1469" class="Bound">R</a><a id="1547" class="Symbol">)</a>
  <a id="1551" href="Relation.Binary.Construct.Union.html#1491" class="Function">symmetric</a> <a id="1561" href="Relation.Binary.Construct.Union.html#1561" class="Bound">L-sym</a> <a id="1567" href="Relation.Binary.Construct.Union.html#1567" class="Bound">R-sym</a> <a id="1573" class="Symbol">=</a> <a id="1575" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1577" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="1582" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1584" href="Relation.Binary.Construct.Union.html#1561" class="Bound">L-sym</a> <a id="1590" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1592" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="1597" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1599" href="Relation.Binary.Construct.Union.html#1567" class="Bound">R-sym</a> <a id="1605" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

  <a id="1610" href="Relation.Binary.Construct.Union.html#1610" class="Function">respects</a> <a id="1619" class="Symbol">:</a> <a id="1621" class="Symbol">∀</a> <a id="1623" class="Symbol">{</a><a id="1624" href="Relation.Binary.Construct.Union.html#1624" class="Bound">p</a><a id="1625" class="Symbol">}</a> <a id="1627" class="Symbol">{</a><a id="1628" href="Relation.Binary.Construct.Union.html#1628" class="Bound">P</a> <a id="1630" class="Symbol">:</a> <a id="1632" href="Relation.Binary.Construct.Union.html#1463" class="Bound">A</a> <a id="1634" class="Symbol">→</a> <a id="1636" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1640" href="Relation.Binary.Construct.Union.html#1624" class="Bound">p</a><a id="1641" class="Symbol">}</a> <a id="1643" class="Symbol">→</a>
             <a id="1658" href="Relation.Binary.Construct.Union.html#1628" class="Bound">P</a> <a id="1660" href="Relation.Binary.Definitions.html#4853" class="Function Operator">Respects</a> <a id="1669" href="Relation.Binary.Construct.Union.html#1455" class="Bound">L</a> <a id="1671" class="Symbol">→</a> <a id="1673" href="Relation.Binary.Construct.Union.html#1628" class="Bound">P</a> <a id="1675" href="Relation.Binary.Definitions.html#4853" class="Function Operator">Respects</a> <a id="1684" href="Relation.Binary.Construct.Union.html#1469" class="Bound">R</a> <a id="1686" class="Symbol">→</a> <a id="1688" href="Relation.Binary.Construct.Union.html#1628" class="Bound">P</a> <a id="1690" href="Relation.Binary.Definitions.html#4853" class="Function Operator">Respects</a> <a id="1699" class="Symbol">(</a><a id="1700" href="Relation.Binary.Construct.Union.html#1455" class="Bound">L</a> <a id="1702" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1704" href="Relation.Binary.Construct.Union.html#1469" class="Bound">R</a><a id="1705" class="Symbol">)</a>
  <a id="1709" href="Relation.Binary.Construct.Union.html#1610" class="Function">respects</a> <a id="1718" href="Relation.Binary.Construct.Union.html#1718" class="Bound">resp-L</a> <a id="1725" href="Relation.Binary.Construct.Union.html#1725" class="Bound">resp-R</a> <a id="1732" class="Symbol">=</a> <a id="1734" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="1736" href="Relation.Binary.Construct.Union.html#1718" class="Bound">resp-L</a> <a id="1743" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="1745" href="Relation.Binary.Construct.Union.html#1725" class="Bound">resp-R</a> <a id="1752" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

<a id="1755" class="Keyword">module</a> <a id="1762" href="Relation.Binary.Construct.Union.html#1762" class="Module">_</a> <a id="1764" class="Symbol">{</a><a id="1765" href="Relation.Binary.Construct.Union.html#1765" class="Bound">≈</a> <a id="1767" class="Symbol">:</a> <a id="1769" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="1773" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1775" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a><a id="1777" class="Symbol">}</a> <a id="1779" class="Symbol">(</a><a id="1780" href="Relation.Binary.Construct.Union.html#1780" class="Bound">L</a> <a id="1782" class="Symbol">:</a> <a id="1784" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="1788" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1790" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="1792" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="1794" class="Symbol">)</a> <a id="1796" class="Symbol">(</a><a id="1797" href="Relation.Binary.Construct.Union.html#1797" class="Bound">R</a> <a id="1799" class="Symbol">:</a> <a id="1801" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="1805" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1807" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="1809" href="Relation.Binary.Construct.Union.html#586" class="Generalizable">ℓ₃</a><a id="1811" class="Symbol">)</a> <a id="1813" class="Keyword">where</a>

  <a id="1822" href="Relation.Binary.Construct.Union.html#1822" class="Function">respˡ</a> <a id="1828" class="Symbol">:</a> <a id="1830" href="Relation.Binary.Construct.Union.html#1780" class="Bound">L</a> <a id="1832" href="Relation.Binary.Definitions.html#5174" class="Function Operator">Respectsˡ</a> <a id="1842" href="Relation.Binary.Construct.Union.html#1765" class="Bound">≈</a> <a id="1844" class="Symbol">→</a> <a id="1846" href="Relation.Binary.Construct.Union.html#1797" class="Bound">R</a> <a id="1848" href="Relation.Binary.Definitions.html#5174" class="Function Operator">Respectsˡ</a> <a id="1858" href="Relation.Binary.Construct.Union.html#1765" class="Bound">≈</a> <a id="1860" class="Symbol">→</a> <a id="1862" class="Symbol">(</a><a id="1863" href="Relation.Binary.Construct.Union.html#1780" class="Bound">L</a> <a id="1865" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="1867" href="Relation.Binary.Construct.Union.html#1797" class="Bound">R</a><a id="1868" class="Symbol">)</a> <a id="1870" href="Relation.Binary.Definitions.html#5174" class="Function Operator">Respectsˡ</a> <a id="1880" href="Relation.Binary.Construct.Union.html#1765" class="Bound">≈</a>
  <a id="1884" href="Relation.Binary.Construct.Union.html#1822" class="Function">respˡ</a> <a id="1890" href="Relation.Binary.Construct.Union.html#1890" class="Bound">Lˡ</a> <a id="1893" href="Relation.Binary.Construct.Union.html#1893" class="Bound">Rˡ</a> <a id="1896" href="Relation.Binary.Construct.Union.html#1896" class="Bound">x≈y</a> <a id="1900" class="Symbol">=</a> <a id="1902" href="Data.Sum.Base.html#1362" class="Function">Sum.map</a> <a id="1910" class="Symbol">(</a><a id="1911" href="Relation.Binary.Construct.Union.html#1890" class="Bound">Lˡ</a> <a id="1914" href="Relation.Binary.Construct.Union.html#1896" class="Bound">x≈y</a><a id="1917" class="Symbol">)</a> <a id="1919" class="Symbol">(</a><a id="1920" href="Relation.Binary.Construct.Union.html#1893" class="Bound">Rˡ</a> <a id="1923" href="Relation.Binary.Construct.Union.html#1896" class="Bound">x≈y</a><a id="1926" class="Symbol">)</a>

<a id="1929" class="Keyword">module</a> <a id="1936" href="Relation.Binary.Construct.Union.html#1936" class="Module">_</a> <a id="1938" class="Symbol">{</a><a id="1939" href="Relation.Binary.Construct.Union.html#1939" class="Bound">≈</a> <a id="1941" class="Symbol">:</a> <a id="1943" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="1947" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="1949" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a><a id="1951" class="Symbol">}</a> <a id="1953" class="Symbol">(</a><a id="1954" href="Relation.Binary.Construct.Union.html#1954" class="Bound">L</a> <a id="1956" class="Symbol">:</a> <a id="1958" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="1962" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1964" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="1966" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="1968" class="Symbol">)</a> <a id="1970" class="Symbol">(</a><a id="1971" href="Relation.Binary.Construct.Union.html#1971" class="Bound">R</a> <a id="1973" class="Symbol">:</a> <a id="1975" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="1979" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="1981" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="1983" href="Relation.Binary.Construct.Union.html#586" class="Generalizable">ℓ₃</a><a id="1985" class="Symbol">)</a> <a id="1987" class="Keyword">where</a>

  <a id="1996" href="Relation.Binary.Construct.Union.html#1996" class="Function">respʳ</a> <a id="2002" class="Symbol">:</a> <a id="2004" href="Relation.Binary.Construct.Union.html#1954" class="Bound">L</a> <a id="2006" href="Relation.Binary.Definitions.html#5009" class="Function Operator">Respectsʳ</a> <a id="2016" href="Relation.Binary.Construct.Union.html#1939" class="Bound">≈</a> <a id="2018" class="Symbol">→</a> <a id="2020" href="Relation.Binary.Construct.Union.html#1971" class="Bound">R</a> <a id="2022" href="Relation.Binary.Definitions.html#5009" class="Function Operator">Respectsʳ</a> <a id="2032" href="Relation.Binary.Construct.Union.html#1939" class="Bound">≈</a> <a id="2034" class="Symbol">→</a> <a id="2036" class="Symbol">(</a><a id="2037" href="Relation.Binary.Construct.Union.html#1954" class="Bound">L</a> <a id="2039" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="2041" href="Relation.Binary.Construct.Union.html#1971" class="Bound">R</a><a id="2042" class="Symbol">)</a> <a id="2044" href="Relation.Binary.Definitions.html#5009" class="Function Operator">Respectsʳ</a> <a id="2054" href="Relation.Binary.Construct.Union.html#1939" class="Bound">≈</a>
  <a id="2058" href="Relation.Binary.Construct.Union.html#1996" class="Function">respʳ</a> <a id="2064" href="Relation.Binary.Construct.Union.html#2064" class="Bound">Lʳ</a> <a id="2067" href="Relation.Binary.Construct.Union.html#2067" class="Bound">Rʳ</a> <a id="2070" href="Relation.Binary.Construct.Union.html#2070" class="Bound">x≈y</a> <a id="2074" class="Symbol">=</a> <a id="2076" href="Data.Sum.Base.html#1362" class="Function">Sum.map</a> <a id="2084" class="Symbol">(</a><a id="2085" href="Relation.Binary.Construct.Union.html#2064" class="Bound">Lʳ</a> <a id="2088" href="Relation.Binary.Construct.Union.html#2070" class="Bound">x≈y</a><a id="2091" class="Symbol">)</a> <a id="2093" class="Symbol">(</a><a id="2094" href="Relation.Binary.Construct.Union.html#2067" class="Bound">Rʳ</a> <a id="2097" href="Relation.Binary.Construct.Union.html#2070" class="Bound">x≈y</a><a id="2100" class="Symbol">)</a>

<a id="2103" class="Keyword">module</a> <a id="2110" href="Relation.Binary.Construct.Union.html#2110" class="Module">_</a> <a id="2112" class="Symbol">{</a><a id="2113" href="Relation.Binary.Construct.Union.html#2113" class="Bound">≈</a> <a id="2115" class="Symbol">:</a> <a id="2117" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="2121" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2123" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a><a id="2125" class="Symbol">}</a> <a id="2127" class="Symbol">{</a><a id="2128" href="Relation.Binary.Construct.Union.html#2128" class="Bound">L</a> <a id="2130" class="Symbol">:</a> <a id="2132" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="2136" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2138" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="2140" class="Symbol">}</a> <a id="2142" class="Symbol">{</a><a id="2143" href="Relation.Binary.Construct.Union.html#2143" class="Bound">R</a> <a id="2145" class="Symbol">:</a> <a id="2147" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="2151" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2153" href="Relation.Binary.Construct.Union.html#586" class="Generalizable">ℓ₃</a><a id="2155" class="Symbol">}</a> <a id="2157" class="Keyword">where</a>

  <a id="2166" href="Relation.Binary.Construct.Union.html#2166" class="Function">resp₂</a> <a id="2172" class="Symbol">:</a> <a id="2174" href="Relation.Binary.Construct.Union.html#2128" class="Bound">L</a> <a id="2176" href="Relation.Binary.Definitions.html#5337" class="Function Operator">Respects₂</a> <a id="2186" href="Relation.Binary.Construct.Union.html#2113" class="Bound">≈</a> <a id="2188" class="Symbol">→</a> <a id="2190" href="Relation.Binary.Construct.Union.html#2143" class="Bound">R</a> <a id="2192" href="Relation.Binary.Definitions.html#5337" class="Function Operator">Respects₂</a> <a id="2202" href="Relation.Binary.Construct.Union.html#2113" class="Bound">≈</a> <a id="2204" class="Symbol">→</a> <a id="2206" class="Symbol">(</a><a id="2207" href="Relation.Binary.Construct.Union.html#2128" class="Bound">L</a> <a id="2209" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="2211" href="Relation.Binary.Construct.Union.html#2143" class="Bound">R</a><a id="2212" class="Symbol">)</a> <a id="2214" href="Relation.Binary.Definitions.html#5337" class="Function Operator">Respects₂</a> <a id="2224" href="Relation.Binary.Construct.Union.html#2113" class="Bound">≈</a>
  <a id="2228" href="Relation.Binary.Construct.Union.html#2166" class="Function">resp₂</a> <a id="2234" class="Symbol">(</a><a id="2235" href="Relation.Binary.Construct.Union.html#2235" class="Bound">Lʳ</a> <a id="2238" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2240" href="Relation.Binary.Construct.Union.html#2240" class="Bound">Lˡ</a><a id="2242" class="Symbol">)</a> <a id="2244" class="Symbol">(</a><a id="2245" href="Relation.Binary.Construct.Union.html#2245" class="Bound">Rʳ</a> <a id="2248" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2250" href="Relation.Binary.Construct.Union.html#2250" class="Bound">Rˡ</a><a id="2252" class="Symbol">)</a> <a id="2254" class="Symbol">=</a> <a id="2256" href="Relation.Binary.Construct.Union.html#1996" class="Function">respʳ</a> <a id="2262" href="Relation.Binary.Construct.Union.html#2128" class="Bound">L</a> <a id="2264" href="Relation.Binary.Construct.Union.html#2143" class="Bound">R</a> <a id="2266" href="Relation.Binary.Construct.Union.html#2235" class="Bound">Lʳ</a> <a id="2269" href="Relation.Binary.Construct.Union.html#2245" class="Bound">Rʳ</a> <a id="2272" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2274" href="Relation.Binary.Construct.Union.html#1822" class="Function">respˡ</a> <a id="2280" href="Relation.Binary.Construct.Union.html#2128" class="Bound">L</a> <a id="2282" href="Relation.Binary.Construct.Union.html#2143" class="Bound">R</a> <a id="2284" href="Relation.Binary.Construct.Union.html#2240" class="Bound">Lˡ</a> <a id="2287" href="Relation.Binary.Construct.Union.html#2250" class="Bound">Rˡ</a>

<a id="2291" class="Keyword">module</a> <a id="2298" href="Relation.Binary.Construct.Union.html#2298" class="Module">_</a> <a id="2300" class="Symbol">(</a><a id="2301" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2303" class="Symbol">:</a> <a id="2305" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="2309" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2311" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="2313" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a><a id="2315" class="Symbol">)</a> <a id="2317" class="Symbol">(</a><a id="2318" href="Relation.Binary.Construct.Union.html#2318" class="Bound">L</a> <a id="2320" class="Symbol">:</a> <a id="2322" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="2326" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2328" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="2330" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="2332" class="Symbol">)</a> <a id="2334" class="Symbol">(</a><a id="2335" href="Relation.Binary.Construct.Union.html#2335" class="Bound">R</a> <a id="2337" class="Symbol">:</a> <a id="2339" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="2343" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2345" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="2347" href="Relation.Binary.Construct.Union.html#586" class="Generalizable">ℓ₃</a><a id="2349" class="Symbol">)</a> <a id="2351" class="Keyword">where</a>

  <a id="2360" href="Relation.Binary.Construct.Union.html#2360" class="Function">implies</a> <a id="2368" class="Symbol">:</a> <a id="2370" class="Symbol">(</a><a id="2371" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2373" href="Relation.Binary.Core.html#1254" class="Function Operator">⇒</a> <a id="2375" href="Relation.Binary.Construct.Union.html#2318" class="Bound">L</a><a id="2376" class="Symbol">)</a> <a id="2378" href="Data.Sum.Base.html#734" class="Datatype Operator">⊎</a> <a id="2380" class="Symbol">(</a><a id="2381" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2383" href="Relation.Binary.Core.html#1254" class="Function Operator">⇒</a> <a id="2385" href="Relation.Binary.Construct.Union.html#2335" class="Bound">R</a><a id="2386" class="Symbol">)</a> <a id="2388" class="Symbol">→</a> <a id="2390" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2392" href="Relation.Binary.Core.html#1254" class="Function Operator">⇒</a> <a id="2394" class="Symbol">(</a><a id="2395" href="Relation.Binary.Construct.Union.html#2318" class="Bound">L</a> <a id="2397" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="2399" href="Relation.Binary.Construct.Union.html#2335" class="Bound">R</a><a id="2400" class="Symbol">)</a>
  <a id="2404" href="Relation.Binary.Construct.Union.html#2360" class="Function">implies</a> <a id="2412" class="Symbol">=</a> <a id="2414" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="2416" href="Data.Sum.Base.html#784" class="InductiveConstructor">inj₁</a> <a id="2421" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="2424" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="2426" href="Data.Sum.Base.html#809" class="InductiveConstructor">inj₂</a> <a id="2431" href="Function.Base.html#1106" class="Function Operator">∘_</a> <a id="2434" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

  <a id="2439" href="Relation.Binary.Construct.Union.html#2439" class="Function">irreflexive</a> <a id="2451" class="Symbol">:</a> <a id="2453" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2465" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2467" href="Relation.Binary.Construct.Union.html#2318" class="Bound">L</a> <a id="2469" class="Symbol">→</a> <a id="2471" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2483" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2485" href="Relation.Binary.Construct.Union.html#2335" class="Bound">R</a> <a id="2487" class="Symbol">→</a> <a id="2489" href="Relation.Binary.Definitions.html#2241" class="Function">Irreflexive</a> <a id="2501" href="Relation.Binary.Construct.Union.html#2301" class="Bound">≈</a> <a id="2503" class="Symbol">(</a><a id="2504" href="Relation.Binary.Construct.Union.html#2318" class="Bound">L</a> <a id="2506" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="2508" href="Relation.Binary.Construct.Union.html#2335" class="Bound">R</a><a id="2509" class="Symbol">)</a>
  <a id="2513" href="Relation.Binary.Construct.Union.html#2439" class="Function">irreflexive</a> <a id="2525" href="Relation.Binary.Construct.Union.html#2525" class="Bound">L-irrefl</a> <a id="2534" href="Relation.Binary.Construct.Union.html#2534" class="Bound">R-irrefl</a> <a id="2543" href="Relation.Binary.Construct.Union.html#2543" class="Bound">x≈y</a> <a id="2547" class="Symbol">=</a> <a id="2549" href="Data.Sum.Base.html#920" class="Function Operator">[</a> <a id="2551" href="Relation.Binary.Construct.Union.html#2525" class="Bound">L-irrefl</a> <a id="2560" href="Relation.Binary.Construct.Union.html#2543" class="Bound">x≈y</a> <a id="2564" href="Data.Sum.Base.html#920" class="Function Operator">,</a> <a id="2566" href="Relation.Binary.Construct.Union.html#2534" class="Bound">R-irrefl</a> <a id="2575" href="Relation.Binary.Construct.Union.html#2543" class="Bound">x≈y</a> <a id="2579" href="Data.Sum.Base.html#920" class="Function Operator">]</a>

<a id="2582" class="Keyword">module</a> <a id="2589" href="Relation.Binary.Construct.Union.html#2589" class="Module">_</a> <a id="2591" class="Symbol">{</a><a id="2592" href="Relation.Binary.Construct.Union.html#2592" class="Bound">L</a> <a id="2594" class="Symbol">:</a> <a id="2596" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="2600" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2602" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="2604" href="Relation.Binary.Construct.Union.html#580" class="Generalizable">ℓ₁</a><a id="2606" class="Symbol">}</a> <a id="2608" class="Symbol">{</a><a id="2609" href="Relation.Binary.Construct.Union.html#2609" class="Bound">R</a> <a id="2611" class="Symbol">:</a> <a id="2613" href="Relation.Binary.Core.html#766" class="Function">REL</a> <a id="2617" href="Relation.Binary.Construct.Union.html#601" class="Generalizable">A</a> <a id="2619" href="Relation.Binary.Construct.Union.html#615" class="Generalizable">B</a> <a id="2621" href="Relation.Binary.Construct.Union.html#583" class="Generalizable">ℓ₂</a><a id="2623" class="Symbol">}</a> <a id="2625" class="Keyword">where</a>

  <a id="2634" href="Relation.Binary.Construct.Union.html#2634" class="Function">decidable</a> <a id="2644" class="Symbol">:</a> <a id="2646" href="Relation.Binary.Definitions.html#5835" class="Function">Decidable</a> <a id="2656" href="Relation.Binary.Construct.Union.html#2592" class="Bound">L</a> <a id="2658" class="Symbol">→</a> <a id="2660" href="Relation.Binary.Definitions.html#5835" class="Function">Decidable</a> <a id="2670" href="Relation.Binary.Construct.Union.html#2609" class="Bound">R</a> <a id="2672" class="Symbol">→</a> <a id="2674" href="Relation.Binary.Definitions.html#5835" class="Function">Decidable</a> <a id="2684" class="Symbol">(</a><a id="2685" href="Relation.Binary.Construct.Union.html#2592" class="Bound">L</a> <a id="2687" href="Relation.Binary.Construct.Union.html#714" class="Function Operator">∪</a> <a id="2689" href="Relation.Binary.Construct.Union.html#2609" class="Bound">R</a><a id="2690" class="Symbol">)</a>
  <a id="2694" href="Relation.Binary.Construct.Union.html#2634" class="Function">decidable</a> <a id="2704" href="Relation.Binary.Construct.Union.html#2704" class="Bound">L?</a> <a id="2707" href="Relation.Binary.Construct.Union.html#2707" class="Bound">R?</a> <a id="2710" href="Relation.Binary.Construct.Union.html#2710" class="Bound">x</a> <a id="2712" href="Relation.Binary.Construct.Union.html#2712" class="Bound">y</a> <a id="2714" class="Symbol">=</a> <a id="2716" href="Relation.Binary.Construct.Union.html#2704" class="Bound">L?</a> <a id="2719" href="Relation.Binary.Construct.Union.html#2710" class="Bound">x</a> <a id="2721" href="Relation.Binary.Construct.Union.html#2712" class="Bound">y</a> <a id="2723" href="Relation.Nullary.Sum.html#946" class="Function Operator">⊎-dec</a> <a id="2729" href="Relation.Binary.Construct.Union.html#2707" class="Bound">R?</a> <a id="2732" href="Relation.Binary.Construct.Union.html#2710" class="Bound">x</a> <a id="2734" href="Relation.Binary.Construct.Union.html#2712" class="Bound">y</a>
</pre></body></html>